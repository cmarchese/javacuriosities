// Importamos los paquetes de JavaCup
import java_cup.runtime.*;
import ar.com.javacuriosities.calculator.model.*;

// Aqui podemos agregar codigo especifico para el parser
parser code {:
	// public static void main(String args[]) throws Exception {
	// new parser(new ArithmeticLexer(System.in)).parse();
	// }
:}

// Tanto los terminal como los non terminal pueden definir un nombre de clase (Igualmente esto es opcional) para asociar a ese token
terminal Integer NUMBER;

// Aquí definimos terminales, que luego usaremos en las reglas de producción
terminal PLUS, SUBTRACT, MULTIPLICATION, DIVISION, L_PARENTHESIS, R_PARENTHESIS, SEMICOLON;

non terminal Document Document;

non terminal Expression ExpressionLine, PlusOrSubtractExpression, MultiplicationOrDivisionExpression, NumberExpression;

start with Document;

Document ::= 
	Document:prevDocument ExpressionLine:expr 
		{:
			Document document = new Document(prevDocument);
			document.addExpression(expr);
			RESULT = document;
		:}| 
	ExpressionLine:expr
		{:
			Document document = new Document();
			document.addExpression(expr);
			RESULT = document;
		:};
		
ExpressionLine ::= 
	PlusOrSubtractExpression:expr SEMICOLON
		{:
			RESULT = expr;
		:};

PlusOrSubtractExpression ::= 
	PlusOrSubtractExpression:lhs PLUS MultiplicationOrDivisionExpression:rhs 
		{:
  			RESULT = new PlusExpression(lhs, rhs); 
		:} |
	PlusOrSubtractExpression:lhs SUBTRACT MultiplicationOrDivisionExpression:rhs 
		{:
  			RESULT = new SubtractExpression(lhs, rhs); 
		:} |
	MultiplicationOrDivisionExpression:expr
		{:
			RESULT = expr;
		:};

MultiplicationOrDivisionExpression ::= 
	MultiplicationOrDivisionExpression:lhs MULTIPLICATION NumberExpression:rhs 
		{:
  			RESULT = new MultiplicationExpression(lhs, rhs); 
		:} |
	MultiplicationOrDivisionExpression:lhs DIVISION NumberExpression:rhs 
		{:
  			RESULT = new DivisionExpression(lhs, rhs); 
		:} |
	NumberExpression:expr
		{:
			RESULT = expr;
		:};
		
NumberExpression::= 
	L_PARENTHESIS PlusOrSubtractExpression:expr R_PARENTHESIS
		{:
			RESULT = new EnclosedExpression(expr); 
		:} |
	NUMBER:value 
		{:
			RESULT = new NumberExpression(value); 
		:};